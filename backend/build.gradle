plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'pmd'
	id 'jacoco'
}

group = 'com.higorcraco'
version = '0.0.1-SNAPSHOT'
description = 'Projeto de votação FullStack'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-h2console'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	pmd 'net.sourceforge.pmd:pmd-java:6.55.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

pmd {
	toolVersion = '6.55.0'
	ignoreFailures = false
	ruleSetFiles = files('config/pmd/ruleset.xml')

	sourceSets {
		main
	}
}

jacoco {
	toolVersion = '0.8.10'
}

tasks.named('test') {
	finalizedBy jacocoTestReport
}

tasks.named('jacocoTestReport') {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'PACKAGE'
			excludes = ['com.higorcraco.votacao_fullstack.config']

			limit {
				minimum = 0.90
			}
		}

		rule {
			element = 'CLASS'
			excludes = [
				'**/config/**',
				'**/exception/**',
				'**/VotacaoFullstackApplication'
			]

			limit {
				minimum = 0.90
			}
		}
	}
}
